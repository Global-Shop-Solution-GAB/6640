Program.Sub.ScreenSU.Start
Gui.Form..Create(BaseForm)
Gui.Form..Caption("Print Inventory Label")
Gui.Form..Size(7005,1170)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..AlwaysOnTop(True)
Gui.Form..FontName("Tahoma")
Gui.Form..FontSize(8.25)
Gui.Form..ControlBox(True)
Gui.Form..MaxButton(False)
Gui.Form..MinButton(True)
Gui.Form..MousePointer(0)
Gui.Form..Moveable(True)
Gui.Form..Sizeable(False)
Gui.Form..ShowInTaskBar(True)
Gui.Form..TitleBar(True)
Gui.Form..Event(UnLoad,Unload)
Gui.Form.txtPart.Create(TextBox,"",True,2205,300,0,105,465,True,0,"Tahoma",8.25,,1)
Gui.Form.txtPart.MaxLength(20)
Gui.Form.txtPart.Event(LostFocus,txtPart_LostFocus)
Gui.Form.txtPart.TabStop(True)
Gui.Form.txtPart.TabIndex(1)
Gui.Form.txtLoc.Create(TextBox,"",True,420,300,0,2370,465,True,0,"Tahoma",8.25,,1)
Gui.Form.txtLoc.Event(LostFocus,txtLoc_LostFocus)
Gui.Form.txtLoc.TabStop(True)
Gui.Form.txtLoc.TabIndex(2)
Gui.Form.lbl1.Create(Label,"Part / Rev",True,735,195,0,120,255,True,0,"Tahoma",8.25,,0,0)
Gui.Form.lbl1.BorderStyle(0)
Gui.Form.lbl2.Create(Label,"Loc",True,240,195,0,2385,255,True,0,"Tahoma",8.25,,0,0)
Gui.Form.lbl2.BorderStyle(0)
Gui.Form.txtBin.Create(TextBox,"",True,1080,300,0,3705,465,True,0,"Tahoma",8.25,,1)
Gui.Form.txtBin.Event(LostFocus,txtBin_LostFocus)
Gui.Form.txtBin.TabStop(True)
Gui.Form.txtBin.TabIndex(4)
Gui.Form.lbl3.Create(Label,"Bin",True,210,195,0,3735,255,True,0,"Tahoma",8.25,,0,0)
Gui.Form.lbl3.BorderStyle(0)
Gui.Form.cmdPartLocLookup.Create(Button)
Gui.Form.cmdPartLocLookup.Enabled(True)
Gui.Form.cmdPartLocLookup.Visible(True)
Gui.Form.cmdPartLocLookup.Zorder(0)
Gui.Form.cmdPartLocLookup.Size(345,345)
Gui.Form.cmdPartLocLookup.Position(2850,450)
Gui.Form.cmdPartLocLookup.Caption("")
Gui.Form.cmdPartLocLookup.FontName("Tahoma")
Gui.Form.cmdPartLocLookup.FontSize(8.25)
Gui.Form.cmdPartLocLookup.SvgPicture("icon_browser_color")
Gui.Form.cmdPartLocLookup.Event(Click,cmdPartLocLookup_Click)
Gui.Form.cmdPartLocLookup.TabStop(True)
Gui.Form.cmdPartLocLookup.TabIndex(3)
Gui.Form.cmdBinLookup.Create(Button)
Gui.Form.cmdBinLookup.Enabled(True)
Gui.Form.cmdBinLookup.Visible(True)
Gui.Form.cmdBinLookup.Zorder(0)
Gui.Form.cmdBinLookup.Size(345,345)
Gui.Form.cmdBinLookup.Position(4845,450)
Gui.Form.cmdBinLookup.Caption("")
Gui.Form.cmdBinLookup.FontName("Tahoma")
Gui.Form.cmdBinLookup.FontSize(8.25)
Gui.Form.cmdBinLookup.SvgPicture("icon_browser_color")
Gui.Form.cmdBinLookup.Event(Click,cmdBinLookup_Click)
Gui.Form.cmdBinLookup.TabStop(True)
Gui.Form.cmdBinLookup.TabIndex(5)
Gui.Form.cmdPrint.Create(Button)
Gui.Form.cmdPrint.Enabled(True)
Gui.Form.cmdPrint.Visible(True)
Gui.Form.cmdPrint.Zorder(0)
Gui.Form.cmdPrint.Size(1125,345)
Gui.Form.cmdPrint.Position(5670,450)
Gui.Form.cmdPrint.Caption("Print")
Gui.Form.cmdPrint.FontName("Tahoma")
Gui.Form.cmdPrint.FontSize(8.25)
Gui.Form.cmdPrint.SvgPicture("icon_printer_color")
Gui.Form.cmdPrint.Event(Click,cmdPrint_Click)
Gui.Form.cmdPrint.TabStop(True)
Gui.Form.cmdPrint.TabIndex(6)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try
'Josh Withrow
'2022-02-18 for Key Gas
'Customer requested label that consolidates PART, LOC, BIN with summed QTY across all ITEM_MASTER
'records to represent total qty of part in the bin regardless of LOT/HEAT/SERIAL.

'Hooks...
'	GUI
'		Populate 15710
'		Script 1/2/3 - 15760
'		Passed Vars - Script 1/2/3 - 000016 / 000020 / 000021
'	PO Receipts
'		Populate 15020
'		Script 1/2/3 - 15047 / 15048 / 15049
'		Passed Vars - Script 1/2/3 - 000316 / 000317 / 000318
'	WIP to FG
'		Populate 16492
'		Script 1/2/3 - 16512 / 16514 / 16516
'		Passed Vars - Script 1/2/3 - 000260 / 000261 / 000262

F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	
	'Legacy GUI...  set label of Script 1 to "Inv Label"
	F.Intrinsic.Control.Case(15710)
		V.Passed.000016.Set("Inv Label")
		F.Intrinsic.Control.End
		
	'PO Receipts...  set label of Script 1 to "Inv Label"
	F.Intrinsic.Control.Case(15020)
		V.Passed.000316.Set("Inv Label")
		F.Intrinsic.Control.End
		
	'WIP To Finished Goods...  set label of Script 1 to "Inv Label"
	F.Intrinsic.Control.Case(16492)
		V.Passed.000260.Set("Inv Label")
		F.Intrinsic.Control.End
		
F.Intrinsic.Control.EndSelect

'If we got here, present the Form and set focus to the Part field, first presented.
F.ODBC.Connection!con.OpenCompanyConnection(500)
Gui.Form..Show()
Gui.Form.txtPart.SetFocus

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Catching.Start
V.Local..BulkDeclareString(sError)
F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}",V.Caller.ScriptFile,V.Ambient.NewLine,v.Args.sub,V.Args.errorno,V.args.errordesc,V.Local.sError)
F.Intrinsic.UI.Msgbox(V.Local.sError,"GAB Execution Error")
F.Intrinsic.Control.End
Program.Sub.Catching.End

Program.Sub.Unload.Start
F.Intrinsic.Control.Try

Gui.Form..Visible(false)
F.ODBC.Connection!con.Close
F.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.Unload.End

Program.Sub.txtPart_LostFocus.Start
'Validate Part Number
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSQL,sRet)

F.Intrinsic.Control.If(V.Screen.Form!txtPart.Text.Trim,=,"")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("select count(*) from v_inventory_all where part='{0}'",V.Screen.Form!txtPart.Text,V.Local.sSQL)
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,=,"0")
	F.Intrinsic.UI.Msgbox("Part not found.","Notice")
	Gui.Form.txtPart.Text("")
	Gui.Form.txtPart.SetFocus
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.txtPart_LostFocus.End

Program.Sub.txtLoc_LostFocus.Start
'Validate Part/Loc
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSQL,sRet)

F.Intrinsic.Control.If(V.Screen.Form!txtLoc.Text.Trim,=,"")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("select count(*) from v_inventory_all where part='{0}' and location='{1}'",V.Screen.Form!txtPart.Text,V.Screen.Form!txtLoc.Text,V.Local.sSQL)
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,=,"0")
	F.Intrinsic.UI.Msgbox("Part/Loc not found.","Notice")
	Gui.Form.txtLoc.Text("")
	Gui.Form.txtLoc.SetFocus
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.txtLoc_LostFocus.End

Program.Sub.txtBin_LostFocus.Start
'Validate Bin or Part/Loc/Bin Combo
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSQL,sRet)

F.Intrinsic.Control.If(V.Screen.Form!txtBin.Text.Trim,=,"")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("select count(*) from v_bin_master where bin='{0}' and location='{1}'",V.Screen.Form!txtBin.Text,V.Screen.Form!txtLoc.Text,V.Local.sSQL)
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,=,"0")
	F.Intrinsic.UI.Msgbox("Bin/Loc not found.","Notice")
	Gui.Form.txtBin.Text("")
	Gui.Form.txtBin.SetFocus
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("select count(*) from v_item_master where part='{0}' and location='{1}' and bin='{2}'",V.Screen.Form!txtPart.Text,V.Screen.Form!txtLoc.Text,V.Screen.Form!txtBin.Text,V.Local.sSQL)
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,=,"0")
	F.Intrinsic.UI.Msgbox("Part/Loc/Bin combination not found.","Notice")
	Gui.Form.txtBin.SetFocus
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.txtBin_LostFocus.End

Program.Sub.cmdPartLocLookup_Click.Start
'Part Browser
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSql,sTitles,sWidths,sRet)

V.Local.sSql.Set("select A.PART, A.LOCATION AS LOCN, B.DESCRIPTION, A.BIN, SUM(A.QUANTITY) AS QTY_ON_LABEL from V_ITEM_MASTER A left outer join V_INVENTORY_ALL B on A.PART=B.PART and A.LOCATION=B.LOCATION GROUP BY A.PART, A.LOCATION, B.DESCRIPTION, A.BIN order by A.PART, A.LOCATION, A.BIN")
V.Local.sTitles.Set("Part*!*Location*!*Description*!*Bin*!*Quantity")
V.Local.sWidths.Set("1000*!*300*!*1800*!*500*!*750")

F.Intrinsic.String.Split(V.Local.sTitles,"*!*",V.Local.sTitles)
F.Intrinsic.String.Split(V.Local.sWidths,"*!*",V.Local.sWidths)
F.Intrinsic.UI.SetBrowserHotTypeAhead(True)
F.Intrinsic.UI.Browser("Select Inventory Record","con",V.Local.sSql,V.Local.sTitles,V.Local.sWidths,9000,8000,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,<>,"***CANCEL***")
	F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
	Gui.Form.txtPart.Text(V.Local.sRet(0))
	Gui.Form.txtLoc.Text(V.Local.sRet(1))
	Gui.Form.txtBin.Text(V.Local.sRet(3))
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.cmdPartLocLookup_Click.End

Program.Sub.cmdBinLookup_Click.Start
'Bin Browser
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSql,sTitles,sWidths,sRet)

V.Local.sSql.Set("select A.BIN, A.LOCATION LOCN, B.BIN_DESCRIPTION from V_ITEM_MASTER A left outer join V_BIN_MASTER B on A.BIN=B.BIN and A.LOCATION=B.LOCATION order by LOCN, A.BIN")
V.Local.sTitles.Set("Bin*!*Locn*!*Bin Description")
V.Local.sWidths.Set("700*!*300*!*1800")

F.Intrinsic.String.Split(V.Local.sTitles,"*!*",V.Local.sTitles)
F.Intrinsic.String.Split(V.Local.sWidths,"*!*",V.Local.sWidths)
F.Intrinsic.UI.SetBrowserHotTypeAhead(True)
F.Intrinsic.UI.Browser("Select Bin","con",V.Local.sSql,V.Local.sTitles,V.Local.sWidths,5000,8000,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,<>,"***CANCEL***")
	F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
	Gui.Form.txtBin.Text(V.Local.sRet(0))
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.cmdBinLookup_Click.End

Program.Sub.cmdPrint_Click.Start
F.Intrinsic.Control.Try

V.Local..BulkDeclareString(sSQL,sRet,sReportPath)
V.Local..BulkDeclareLong(iBIRunID,iBILogID,iBIRptID)

F.Intrinsic.String.Build("select count(*) from v_item_master where part='{0}' and location='{1}' and bin='{2}'",V.Screen.Form!txtPart.Text,V.Screen.Form!txtLoc.Text,V.Screen.Form!txtBin.Text,V.Local.sSQL)
F.ODBC.Connection!con.ExecuteAndReturn(V.Local.sSQL,V.Local.sRet)
F.Intrinsic.Control.If(V.Local.sRet,=,"0")
	F.Intrinsic.UI.Msgbox("Part/Loc/Bin combination not found.","Notice")
	Gui.Form.txtPart.SetFocus
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.EndIf

'Report Details...
F.Intrinsic.String.Concat(V.Caller.BusintDir,"\Labels\Custom\GAB_6640_InventoryPartLabel_Codesoft.lab",V.Local.sReportPath)
F.Global.BI.InitializeReport("Inventory Part Label Codesoft (6640)",V.Local.sReportPath,V.Local.iBIRptID)

'Getting Run ID...
F.Global.BI.GetRunID(V.Local.iBIRunID)
'Getting Log ID, start logging...
F.Global.BI.StartLogging(V.Local.iBIRunID,V.Local.iBIRptID,-1,"",V.Local.iBILogID)

'Fetch Label Data...
F.Intrinsic.String.Build("select A.PART, A.LOCATION AS LOCN, B.DESCRIPTION, A.BIN, SUM(A.QUANTITY) AS QTY_ON_LABEL, '{3}' AS USER_ID, B.UM_INVENTORY AS INV_UM from V_ITEM_MASTER A left outer join V_INVENTORY_ALL B on A.PART=B.PART and A.LOCATION=B.LOCATION where A.PART='{0}' and A.LOCATION='{1}' and A.BIN='{2}' GROUP BY A.PART, A.LOCATION, B.DESCRIPTION, A.BIN, B.UM_INVENTORY",V.Screen.Form!txtPart.Text,V.Screen.Form!txtLoc.Text,V.Screen.Form!txtBin.Text,V.Caller.User,V.Local.sSql)
F.Data.DataTable.CreateFromSQL("dtData","con",V.Local.sSQL)

F.Intrinsic.Control.If(V.DataTable.dtData.RowCount--,<>,-1)
    'Printing Codesoft label
    F.Global.BI.PrintCodesoftLabelFromDataTable(V.Local.iBIRunID,V.Local.iBILogID,"dtData","",True)
    'Stopping Logging
	F.Global.BI.StopLogging(V.Local.iBILogID)
F.Intrinsic.Control.EndIf

Function.Intrinsic.Control.CallSub(Reset)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.cmdPrint_Click.End

Program.Sub.Reset.Start
F.Intrinsic.Control.Try

Gui.Form.txtPart.Text("")
Gui.Form.txtLoc.Text("")
Gui.Form.txtBin.Text("")
Gui.Form.txtPart.SetFocus

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Catching,"Sub",V.Ambient.CurrentSubroutine,"ErrorNo",V.Ambient.ErrorNumber,"ErrorDesc",V.Ambient.ErrorDescription) 
F.Intrinsic.Control.EndTry
Program.Sub.Reset.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$jwithrow$}$20220217170905660$}$D95tSYIikIvF+oq+s87EtBQePACEH+EyDs2gPmW3WNrWr/5UtCrC8KmCQmxPIWlSqCx0rSGUwFHAVEZhBQJr5A==
Program.Sub.Comments.End